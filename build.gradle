import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java-library'
    id 'maven-publish'
}

group "xyz.oribuin.skyhoppers"
version "1.0.5"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.rosewooddev.io/repository/public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://nexus.iridiumdevelopment.net/repository/maven-releases/" }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.glaremasters.me/repository/towny/' }


    maven {
        url = 'https://repo.rosewooddev.io/repository/maven-public'
        credentials {
            username project.mavenUser
            password project.mavenPassword
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'

    // Stacker plugins
    compileOnly 'dev.rosewood:rosestacker:1.5.0'
    compileOnly 'com.bgsoftware:wildstacker:3.1.1'

    // Plugin Dependencies
    compileOnly 'world.bentobox:bentobox:1.9.2-SNAPSHOT'
//    compileOnly 'com.github.TechFortress:GriefPrevention:16.18' // Fuck this plugin and how it's coded
    compileOnly 'com.iridium:IridiumSkyblock:3.2.8'
    compileOnly 'com.github.Angeschossen:LandsAPI:6.22.0'
    compileOnly 'com.palmergames.bukkit.towny:towny:0.98.2.0'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.7'

    // Frameworks & MySQL APIs
    implementation 'dev.rosewood:rosegarden:1.1.0.48-SNAPSHOT'
    implementation('dev.triumphteam:triumph-gui:3.1.3') {
        exclude group: 'com.google.code.gson', module: 'gson' // Already included in spigot-api
    }
}

shadowJar {
    archiveClassifier.set(null)
    relocate('dev.rosewood.rosegarden', "xyz.oribuin.skyhoppers.libs.rosegarden")
    relocate('dev.triumphteam.gui', "xyz.oribuin.skyhoppers.libs.gui")
}


processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

tasks.getByName("build").dependsOn("shadowJar")